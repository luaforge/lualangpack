%lexer
%declare{ public string str; public int blocknest; }
%token NAME { public string s; }
%token LITERAL : NAME { 
   public LITERAL( string a ){ s = a; }
}
%token NUMBER { public double n; }
%token COMMENT;

 [ \n\r\t\\~`!@$&?|]  ;
 ","			      %COMMA
 ":"			      %COLON
 ";"			      %SEMICOLON
 "("			      %LPAREN
 ")"			      %RPAREN
 "["			      %LBRACK
 "]"			      %RBRACK
 "{"			      %LBRACE
 "}"			      %RBRACE
 "."			      %DOT
 "+"			      %PLUS
 "-"			      %MINUS
 "*"			      %MULT
 "#"                  %POUND
 "%"                  %MOD
 "/"			      %DIVIDE
 "^"			      %EXP
 "=="			      %EQ
 "~="			      %NEQ
 "<"			      %LT
 "<="			      %LE
 ">"			      %GT
 ">="			      %GE
 "and"			      %AND
 "or"			      %OR
 "not"                %NOT
 "="			      %ASSIGN
 "if"			      %IF
 "then"			      %THEN
 "else"			      %ELSE
 "elseif"		      %ELSEIF
 "end"  		      %END
 "while"		      %WHILE
 "for"			      %FOR
 "do"			      %DO
 "in"			      %IN
 "break"		      %BREAK
 "nil"			      %NIL
 "function"		      %FUNCTION
 "repeat"		      %REPEAT
 "until"		      %UNTIL
 "return"		      %RETURN
 "local"		      %LOCAL
 "true"			      %TRUE
 "false"		      %FALSE
 ".."			      %CONCAT
 "..."			      %ELIPSE
 [a-zA-Z_][A-Za-z0-9_]*  %NAME		{ s = yytext; }
 [0-9]+([.][0-9])?[0-9]* %NUMBER    { n = Convert.ToDouble(yytext); }

 \" 				 { yybegin("DLITERAL"); yyl.str = "\"";}
<DLITERAL>  \\\"     { yyl.str += yytext; }
<DLITERAL>  \\\\     { yyl.str += yytext; }
<DLITERAL>  [^\"]    { yyl.str += yytext; }
<DLITERAL>  \"		 { yybegin("YYINITIAL"); yyl.yytext = yyl.str + "\""; return new LITERAL(yyl.yytext); }

"'" 				 { yybegin("SLITERAL"); yyl.str = "\"";}
<SLITERAL>  "\\'"    { yyl.str += yytext; }
<SLITERAL>  \\\\     { yyl.str += yytext; }
<SLITERAL>  [^']     { yyl.str += yytext; }
<SLITERAL>  "'"		 { yybegin("YYINITIAL"); yyl.yytext = yyl.str + "\""; return new LITERAL(yyl.yytext); }

"--[["                      { yybegin("BLOCKCOMMENT"); yyl.blocknest = 0; }
<BLOCKCOMMENT>  "[["        { ++yyl.blocknest; }
<BLOCKCOMMENT>	"]]"	    { if(yyl.blocknest == 0) yybegin("YYINITIAL"); else --yyl.blocknest; } 
<BLOCKCOMMENT>	.	        ; 
<BLOCKCOMMENT>  \n          ;

"--"			        { yybegin("COMMENT"); }
<COMMENT>	    \n	    { yybegin("YYINITIAL"); }
<COMMENT>	    .	    ;
